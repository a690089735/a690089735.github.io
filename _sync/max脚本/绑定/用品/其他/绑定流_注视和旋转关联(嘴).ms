try(destroydialog CRRollout)catch()
rollout CRRollout "创建眼球绑定" width:96 height:184
(
	button btn_cc "创建注视" pos:[8,32] width:80 height:24 toolTip:"选中一把父骨骼,然后点创建,选择的第一个骨骼是眼睛骨骼"
	checkbox chk1 "创建父级" pos:[8,8] width:80 height:16 enabled:false checked:true
	button btn2 "十二旋转" pos:[8,88] width:80 height:24 toolTip:"必须是本工具创建的绑定,先选主控骨骼,然后从最左开始顺时针旋转骨骼,点击十二旋转."
	
fn  setFreezeTransform objs = 
(
	for obj in objs do 
	(
		obj.pos.controller = bezier_position ()
		obj.pos.controller = position_list ()
		obj.pos.controller.Available.controller =  Position_XYZ ()
		obj.pos.controller.Active = 2
		obj.rotation.controller = Euler_XYZ ()
		obj.rotation.controller = rotation_list ()
		obj.rotation.controller.Available.controller = Euler_XYZ ()
		obj.rotation.controller.Active = 2
	)
)

	fn setparamwire sub a b val= --#X_Rotation;#Y_Rotation;#Z_Rotation
	(
		paramWire.connect a.rotation.controller[2][sub] b.rotation.controller[2][sub] val
	)

	on btn_cc pressed do
	with undo on(
		max create mode
		BoneList = selection as array
		for B in BoneList do 
		(	
			size = (b.height+b.width)
			--最好先对骨骼命名
			p = point name:(b.name+"_P") centermarker:on axistripod:off cross:off Box:off size:(size*0.3) wirecolor:green
			p.parent = b.parent;p.transform = b.transform
			r = point name:(b.name+"_R") centermarker:off axistripod:off cross:on Box:off size:(size*0.5) wirecolor:Yellow
			r.transform = b.transform
			r.parent = p
			b.parent = r
			setFreezeTransform #(p,r)
			
			tag = point name:(b.name+"_Tag") parent:b.parent transform:b.children[1].transform centermarker:on axistripod:off cross:on Box:off size:size wirecolor:red
			b.rotation.controller = Euler_XYZ () --解除直接加旋转列表会把内容作为子控制器
			b.rotation.controller = rotation_list ()
			ctrl = b.rotation.controller[2].controller = LookAt_Constraint ()
			b.rotation.controller.Active = 2
			ctrl.appendTarget tag 50
			b.rotation.controller.LookAt_Constraint.controller.upnode_world = off
			b.rotation.controller.LookAt_Constraint.controller.lookat_vector_length = 0
			ctrl.pickUpNode = r
		)
		
	)
	on btn2 pressed do
	with undo on(
		sel = selection as array
		mainCtrl = sel[1].parent
		wireCtrl = for i = 2 to sel.count collect sel[i].parent
		
	-- 		Xvals = #()
--		#("0.5","0.35","0.2","0.1","0.2","0.35","0.5","0.65","0.8","1","0.8","0.65")--圆
		Zvals = #("0.47","0.16","0.07","0.02","0.07","0.16","0.47","0.82","0.94","1","0.94","0.82")--O椭圆
		Yvals = #("0.47","0.16","0.07","0.02","0.07","0.16","0.47","0.82","0.94","1","0.94","0.82")--O椭圆
-- 		Zvals = #("0.5", "0.03","0","0","0","0.03", "0.5", "0.93","1","1","1","0.93")--木头人
-- 		Yvals = #("0.5", "0.03","0","0","0","0.03", "0.5", "0.93","1","1","1","0.93")--木头人	
		setFreezeTransform #(wireCtrl[2],wireCtrl[4])
		
		for i = 1 to wireCtrl.count do 
		(	
	-- 		setparamwire (#X_Rotation) mainCtrl wireCtrl [i] ("X_Rotation*"+Xvals[i] as string)
			setparamwire (#Y_Rotation) mainCtrl wireCtrl [i] ("Y_Rotation*"+Yvals[i])
			setparamwire (#Z_Rotation) mainCtrl wireCtrl [i] ("Z_Rotation*"+Zvals[i])
		)
	)
	on btn3 pressed  do
	with undo on(
		a = selection[1];b = selection[2].parent
		paramWire.connect a.pos.controller[2].Y_Position.controller[#Limited_Controller__Bezier_Float] b.rotation.controller[2].controller[#Y_Rotation] "-Limited_Controller__Bezier_Float/25"
		paramWire.connect a.pos.controller[2].X_Position.controller[#Limited_Controller__Bezier_Float] b.rotation.controller[2].controller[#Z_Rotation] "Limited_Controller__Bezier_Float/25"
	)
	
)

createdialog CRRollout